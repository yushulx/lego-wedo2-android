apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.legowedo2"
        minSdkVersion 21
        targetSdkVersion 28

        def testedWithFirmwareVersion = "1.0.3.0"
        buildConfigField "String", "TESTED_WITH_FIRMWARE_VERSION", "\"${testedWithFirmwareVersion}\""

        def revisionExe = "git rev-list HEAD --count"
        def revision = revisionExe.execute().text

        def gitHashExe = "git rev-parse --short HEAD"
        def gitHash = gitHashExe.execute().text.trim()

        if (revision?.trim()) {
            revision = revision.toInteger()
        } else {
            revision = 1
        }

        versionName = project.version + " (build " + revision + ")"
        versionCode = revision

        buildConfigField "String", "GIT_HASH", "\"${gitHash}\""
        buildConfigField "String", "VERSION", "\"${versionName}\""
    }

    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}